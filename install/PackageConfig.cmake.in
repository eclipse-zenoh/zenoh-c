#
# Copyright (c) 2022 ZettaScale Technology.
#
# This program and the accompanying materials are made available under the
# terms of the Eclipse Public License 2.0 which is available at
# http://www.eclipse.org/legal/epl-2.0, or the Apache License, Version 2.0
# which is available at https://www.apache.org/licenses/LICENSE-2.0.
#
# SPDX-License-Identifier: EPL-2.0 OR Apache-2.0
#
# Contributors:
#   ZettaScale Zenoh team, <zenoh@zettascale.tech>
#

@PACKAGE_INIT@

set(ZENOHC_BUILD_WITH_UNSTABLE_API @ZENOHC_BUILD_WITH_UNSTABLE_API@)
set(ZENOHC_BUILD_WITH_SHARED_MEMORY @ZENOHC_BUILD_WITH_SHARED_MEMORY@)
set(ZENOHC_BUILD_ZENOHD @ZENOHC_BUILD_ZENOHD@)


if(NOT TARGET __zenohc_shared)
    add_library(__zenohc_shared SHARED IMPORTED GLOBAL)
    add_library(zenohc::shared ALIAS __zenohc_shared)
    set_target_properties(__zenohc_shared PROPERTIES
        IMPORTED_NO_SONAME TRUE
        INTERFACE_COMPILE_DEFINITION ZENOHC_DYN_LIB
        IMPORTED_LOCATION "@PACKAGE_ZENOHC_INSTALL_DYLIBDIR@/@DYLIB@"
        INTERFACE_INCLUDE_DIRECTORIES "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@"
    )
    if(NOT ("@IMPLIB@" STREQUAL ""))
        set_property(TARGET __zenohc_shared PROPERTY IMPORTED_IMPLIB "@PACKAGE_CMAKE_INSTALL_LIBDIR@/@IMPLIB@")
    endif()
endif()
if(NOT TARGET __zenohc_static)
    add_library(__zenohc_static STATIC IMPORTED GLOBAL)
    add_library(zenohc::static ALIAS __zenohc_static)
    target_link_libraries(__zenohc_static INTERFACE @NATIVE_STATIC_LIBS@)
    set_target_properties(__zenohc_static PROPERTIES
        IMPORTED_LOCATION "@PACKAGE_CMAKE_INSTALL_LIBDIR@/@STATICLIB@"
        INTERFACE_INCLUDE_DIRECTORIES "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@"
    )
endif()

if(ZENOHC_BUILD_ZENOHD AND (NOT TARGET __zenohd))
    add_executable(__zenohd IMPORTED)
    set_target_properties(__zenohd
		PROPERTIES
		IMPORTED_LOCATION "@PACKAGE_CMAKE_INSTALL_BINDIR@/zenohd"
	)
    add_executable(zenohc::zenohd ALIAS __zenohd)
endif()

if(NOT TARGET zenohc::lib)
    if(@BUILD_SHARED_LIBS@)
        add_library(zenohc::lib ALIAS __zenohc_shared)
    else()
        add_library(zenohc::lib ALIAS __zenohc_static)
    endif()
endif()

